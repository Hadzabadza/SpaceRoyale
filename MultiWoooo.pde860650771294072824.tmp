import oscP5.*;
import netP5.*;

ArrayList<Star> stars;
ArrayList<Planet> planets;
ArrayList<Asteroid> asteroids;
ArrayList<Bullet> bullets;
ArrayList<Ship> ships;
Ship ship;
Ship ship1;
ArrayList<Object> destroyees;
ArrayList<Object> newSpawns;
ArrayList<Object> objects;
long seed=1;
float offx=0;
float offy=0;
float zoom=1;
float zoom1=1;
boolean ship1ZoomIn;
boolean ship1ZoomOut;
PVector mapScreenShift;
boolean mapScreen;
boolean heightColour;
int tSize=50;
Terrain active;
PVector cursor;
OscHub osc;

/*PWindow win; 
PWindow win1;*/
PGraphics leftView;
PGraphics rightView;

PFont pixFont;

void setup() {
  size(1300, 650, P3D);
  surface.setLocation((displayWidth-1300)/2,(displayHeight-650)/2);
  leftView=createGraphics(width/2,height,P3D);
  rightView=createGraphics(width/2,height,P3D);
  pixFont=createFont("Minecraftia-Regular.ttf", 120, true);
  textFont(pixFont, 12);
  
 init();
  
  osc=new OscHub(2);
}

void init(){
  stars=new ArrayList<Star>();
  planets=new ArrayList<Planet>();
  asteroids=new ArrayList<Asteroid>();
  bullets=new ArrayList<Bullet>();
  ships=new ArrayList<Ship>();
  destroyees=new ArrayList<Object>();
  newSpawns=new ArrayList<Object>();
  objects=new ArrayList<Object>();
  
  ship = new Ship();
  ships.add(ship);
  ship1 = new Ship();
  ship1.c=color(255,255,0);
  stars.add(new Star(0, 0));
  mapScreenShift=new PVector(100, 100);
  cursor=new PVector(0.5, 0.5);
}

void draw() {
  objectUpdates();
  for (int i=0; i<ships.size()-1; i++) osc.OMUpdate(i,ships.get(i));
  background(0);
  if (ship1ZoomIn) zoom1*=0.99;
  else if (ship1ZoomOut) zoom1*=1.01;
  leftView.beginDraw();
  leftView.background(50,60,50);
  leftView.camera(ship.pos.x, ship.pos.y, zoom*600, ship.pos.x, ship.pos.y, 0.0, 0.0, 1.0, 0.0);
  for (Object o : objects) {
    o.draw(leftView);
  }
  leftView.endDraw();

  rightView.beginDraw();
  rightView.background(60,60,50);
  rightView.camera(ship1.pos.x, ship1.pos.y, zoom1*600, ship1.pos.x, ship1.pos.y, 0.0, 0.0, 1.0, 0.0);
  for (Object o : objects) {
    o.draw(rightView);
  }
  rightView.stroke(255);

  rightView.endDraw();

  //add the two viewports to your main panel
  image(leftView, 0, 0);
  image(rightView, width/2, 0);
  
  stroke (255);
  strokeWeight(1);
  noFill();
  rect(width/4-50,10,100,10);
  rect(width/2+width/4-50,10,100,10);
  fill(60+195*(1-ship.HP),60+195*ship.HP,0);
  rect(width/4-50,10,100*ship.HP,10);
  fill(60+195*(1-ship1.HP),60+195*ship1.HP,0);
  rect(width/2+width/4-50,10,100*ship1.HP,10);

  strokeWeight(3);
  line (width/2,0,width/2,height);

  if (mapScreen) {
    camera(ship.pos.x, ship.pos.y, (height/2.0) / tan(PI*30.0 / 180.0)*zoom, ship.pos.x, ship.pos.y, 0.0, 0.0, 1.0, 0.0);
    pushMatrix();
    translate(ship.pos.x-width/2*zoom+mapScreenShift.x*zoom, ship.pos.y-height/2*zoom+mapScreenShift.y*zoom, 2);
    scale(zoom);
    rectMode(CENTER);
    if (ship.land!=null) {
      for (Terrain t : ship.land.terrain)
      {
        t.draw();
      }
      Terrain selected=ship.land.pickTile();
      if (selected!=null)
      {
        fill(255);
        text("Current: "+selected.elevation, 50, -40);
        text("Deepness: "+selected.depth, 250, -40);
        text("Index: "+selected.index, 50, -20);
      }
      fill(255);
      textAlign(CENTER);
      text("Avg: "+ship.land.avgHeight, 50, -60);
      text("Max: "+ship.land.maxHeight, 250, -60);
      text("Min: "+ship.land.minHeight, 450, -60);
      if (heightColour)
        text("Height Colouring: ON", 450, -40);
      else
        text("Height Colouring: OFF", 450, -40);
      text("Total Height: "+ship.land.totalHeight, 250, -20);
      //image(ship.land.map, -mapScreenShift.x+(width-ship.land.map.width)/4, -mapScreenShift.y+(height-ship.land.map.height)/4);
      popMatrix();
    } else
    {
      mapScreen=false;
    }
  }
}


void mouseWheel(MouseEvent e) {
  if (e.getCount()>0)
  {
    zoom*=1.01;
  } else
  {
    zoom*=0.99;
  }
}

void mouseReleased() {
  if (mouseButton==RIGHT)
  {
    if (mapScreen)
    {
      heightColour=!heightColour;
    }
  }
  if (mouseButton==LEFT)
  {
    if (mapScreen) 
    {
      Terrain t=ship.land.pickTile();
      if (t!=null)
      {
        t.build();
        t.volcanize();
      }
      if (t==null)
      {
        mapScreen=false;
        active=null;
      }
    } else if (ship.land!=null) {
      mapScreen=true;
      ship.thrust=0;
    }
  }
  if (mouseButton==CENTER)
  {
    zoom=1;
  }
}

void mouseMoved() {
  cursor.x=mouseX;
  cursor.y=mouseY;
}

void objectUpdates() {
  for (Object o : objects) {
    o.update();
  }
  for (int i=destroyees.size()-1; i>=0; i--){
    destroyees.get(i).destroy();
    destroyees.remove(i);
  }
  for (int i=newSpawns.size()-1; i>=0; i--){
    newSpawns.get(i).spawn();
    newSpawns.remove(i);
  }
}

void exit() {
  println("quitting");
  osc.exit();
  super.exit();
}